<html>  
    <head>  
        <title>WebGL Particle Fluid Simulation </title>
        <meta charset='utf-8'>
        <script src="minMatrixb.js" type="text/javascript"></script>  
        <script src="lib.js" type="text/javascript"></script>  
        <script src="fluid.js" type="text/javascript"></script>  
        <script id="Vertex" type="x-shader/x-vertex">  
            attribute vec4 Position;
            void main(void){
                gl_Position = Position;

            }
        </script>  
        <script id="Fill" type="x-shader/x-fragment">  
            precision mediump float;
            void main(void){
                gl_FragColor  = vec4(0.3, 0.5, 0.0, 1.0);
            } 
        </script> 
        <script id="FirstPass.VS" type="x-shader/x-vertex">
            attribute vec3 position;
            varying vec3 worldSpaceCoords;
            uniform mat4 modelViewMatrix;
            uniform mat4 projectionMatrix;
            void main()
            {
                //Set the world space coordinates of the back faces vertices as output.
                worldSpaceCoords = position + vec3(0.5, 0.5, 0.5); //move it from [-0.5;0.5] to [0,1]
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        
        <script id="FirstPass.FS" type="x-shader/x-fragment">
            precision mediump float;
            varying vec3 worldSpaceCoords;

            void main()
            {
                //The fragment's world space coordinates as fragment output.
                gl_FragColor = vec4( worldSpaceCoords.x , worldSpaceCoords.y, worldSpaceCoords.z, 1 );
            }
        </script>
        <script id="SecondPass.VS" type="x-shader/x-vertex">
            attribute vec3 position;
            varying vec3 worldSpaceCoords;
            varying vec4 projectedCoords;
            uniform mat4 modelViewMatrix;
            uniform mat4 modelMatrix;
            uniform mat4 projectionMatrix;
            void main()
            {
                worldSpaceCoords = (modelMatrix * vec4(position + vec3(0.5, 0.5,0.5), 1.0 )).xyz;
                gl_Position = projectionMatrix *  modelViewMatrix * vec4( position, 1.0 );
                projectedCoords =  projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        <script id="SecondPass.FS" type="x-shader/x-fragment">
            precision mediump float;
            varying vec3 worldSpaceCoords;
            varying vec4 projectedCoords;
            uniform sampler2D texure, cubeTex;
            uniform float steps;
            uniform float alphaCorrection;
            const int MAX_STEPS = 512;
            
            //Acts like a texture3D using Z slices and trilinear filtering.
            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               float sliceSize = 1.0 / size;                         // space of 1 slice
               float slicePixelSize = sliceSize / size;              // space of 1 pixel
               float sliceInnerSize = slicePixelSize * (size - 1.0); // space of size pixels
               float zSlice0 = min(floor(texCoord.z * size), size - 1.0);
               float zSlice1 = min(zSlice0 + 1.0, size - 1.0);
               float xOffset = slicePixelSize * 0.5 + texCoord.x * sliceInnerSize;
               float s0 = xOffset + (zSlice0 * sliceSize);
               float s1 = xOffset + (zSlice1 * sliceSize);
               vec4 slice0Color = texture2D(tex, vec2(s0, texCoord.y));
               vec4 slice1Color = texture2D(tex, vec2(s1, texCoord.y));
               float zOffset = mod(texCoord.z * size, 1.0);
              //return mix(slice0Color, slice1Color, zOffset);
              return vec4(1.0,1.0,1.0,1.0);
            }
        
            void main( void ) {

                //Transform the coordinates it from [-1;1] to [0;1]
                vec2 texc = vec2(((projectedCoords.x / projectedCoords.w) + 1.0 ) / 2.0,
                                ((projectedCoords.y / projectedCoords.w) + 1.0 ) / 2.0 );

                //The back position is the world space position stored in the texture.
                vec3 backPos = texture2D(texure, texc).xyz;

                //The front position is the world space position of the second render pass.
                vec3 frontPos = worldSpaceCoords;

                //The direction from the front position to back position.
                vec3 dir = backPos - frontPos;
                
                float rayLength = length(dir); 
                
                //Calculate how long to increment in each step.
                float delta = 1.0 / steps;

                //The increment in each direction for each step.
                vec3 deltaDirection = normalize(dir) * delta;
                float deltaDirectionLength = length(deltaDirection);

                //Start the ray casting from the front position.
                vec3 currentPosition = frontPos;
                
                //The color accumulator.
                vec4 accumulatedColor = vec4(0.0);
                
                //The alpha value accumulated so far.
                float accumulatedAlpha = 0.0;
                
                //How long has the ray travelled so far.
                float accumulatedLength = 0.0;
                
                vec4 colorSample;
                float alphaSample;
                //Perform the ray marching iterations
                for(int i = 0; i < MAX_STEPS; i++)
                {
                    //Get the voxel intensity value from the 3D texture.    
                    colorSample = sampleAs3DTexture( cubeTex,currentPosition,steps );
                  
                    //Allow the alpha correction customization
                    alphaSample = colorSample.a * alphaCorrection;

                    //Perform the composition.
                    accumulatedColor += (1.0 - accumulatedAlpha) * colorSample * alphaSample;
                    
                    //Store the alpha accumulated so far.
                    accumulatedAlpha += alphaSample;
                    
                    //Advance the ray.
                    currentPosition += deltaDirection;
                    accumulatedLength += deltaDirectionLength;
                              
                    //If the length traversed is more than the ray length, or if the alpha accumulated reaches 1.0 then exit.
                    if(accumulatedLength >= rayLength || accumulatedAlpha >= 1.0 )
                        break;
                }

                 gl_FragColor  = vec4(accumulatedColor.rgb,1.0);
                //  gl_FragColor  = vec4(1.0,1.0,1.0,1.0);
                  
            }
        </script>
        


        <script id="Cube.VS" type="x-shader/x-vertex">  
            attribute vec3 Position;
            uniform float Size;
            varying float vSize;
            uniform mat4 ModelviewProjection;
            void main()
            {
                vSize =Size;
                gl_Position = ModelviewProjection * vec4(Position, 1.0);
            }
        </script>  
        <script id="Cube.FS" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D Density;
            uniform vec3 LightPosition ;
            uniform vec3 LightIntensity;
            uniform float Absorption;
            uniform mat4 Modelview;
            uniform float FocalLength;
            uniform vec2 WindowSize;
            uniform vec3 RayOrigin;
            varying float vSize;
            uniform vec3 Scale;

            const float maxDist = sqrt(2.0);
            const int numSamples = 128;
            const float stepSize = maxDist/float(numSamples);
            const int numLightSamples = 32;
            const float lscale = maxDist / float(numLightSamples);
            const float densityFactor = 10.0;
            const int GridDepth= 1;

            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size + texCoord.z, texCoord.y));
               return Color;
            }
            float GetDensity(vec3 pos)
            {
                return sampleAs3DTexture(Density, pos, vSize).x * densityFactor;
            }

            struct Ray {
                vec3 Origin;
                vec3 Dir;
            };

            struct AABB {
                vec3 Min;
                vec3 Max;
            };

            bool IntersectBox(Ray r, AABB aabb, out float t0, out float t1)
            {
                vec3 invR = 1.0 / r.Dir;
                vec3 tbot = invR * (aabb.Min-r.Origin);
                vec3 ttop = invR * (aabb.Max-r.Origin);
                vec3 tmin = min(ttop, tbot);
                vec3 tmax = max(ttop, tbot);
                vec2 t = max(tmin.xx, tmin.yz);
                t0 = max(t.x, t.y);
                t = min(tmax.xx, tmax.yz);
                t1 = min(t.x, t.y);
                return t0 <= t1;
            }
            void main()
            {   
                vec3 rayDirection;
                rayDirection.xy = 2.0 * gl_FragCoord.xy / WindowSize - 1.0;
                rayDirection.z = -FocalLength;
                rayDirection = (vec4(rayDirection, 0) * Modelview).xyz;

                Ray eye = Ray( RayOrigin, normalize(rayDirection) );
                AABB aabb = AABB(vec3(-1.0), vec3(+1.0));
                float L;
                float tnear, tfar;
                IntersectBox(eye, aabb, tnear, tfar);
                if (tnear < 0.0) tnear = 0.0;

                vec3 rayStart = eye.Origin + eye.Dir * tnear;
                vec3 rayStop = eye.Origin + eye.Dir * tfar;
                rayStart = 0.5 * (rayStart + 1.0);
                rayStop = 0.5 * (rayStop + 1.0);

                vec3 pos = rayStart;
                vec3 step = normalize(rayStop-rayStart) * stepSize;
                float travel = distance(rayStop, rayStart);
                float T = 1.0;
                vec3 Lo = vec3(0.0);
                for(int i =0; i< GridDepth; i++){
                    vec3 T = vec3(gl_FragCoord.xy, i);
                    L = sampleAs3DTexture(Density, vec3(T.x,T.y, T.z) * Scale, vSize).r;   
                }       
                gl_FragColor =vec4(1.0,1.0,1.0,1.0);
                //gl_FragColor.a = 1.0 -T;
            }
        </script>
        <script id="Render.Vertex" type="x-shader/x-vertex">  
            attribute vec4 Position;
            uniform float Slice;
            uniform float Size;
            varying float gLayer;
            varying float vSize;  
            varying vec3 vScale;
            uniform vec3 Scale;
            uniform mat4 ModelviewProjection;   
            void main(void){
                gl_Position.xy = Position.xy;
                gl_Position.zw =vec2((2.0/Size * Slice - 1.0) * 0.08 , 1.0);
                gLayer =Slice;
                vSize = Size;
                vScale =Scale;
                gl_Position = ModelviewProjection * gl_Position;
            }
        </script>  
        <script id="Render.Fill" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D Sampler;
            uniform vec3 FillColor;
            varying vec3 vScale;
            varying float gLayer;
            varying float vSize;
            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size + texCoord.z, texCoord.y));
               return Color;
            }
            void main(){
                vec3 T = vec3(gl_FragCoord.xy, gLayer);
                float L = sampleAs3DTexture(Sampler, vec3(T.x,T.y, T.z) * vScale, vSize).r;
                gl_FragColor = vec4(FillColor,L);
                
            }
        </script>

        <script id="Smoke.Vertex" type="x-shader/x-vertex">  
            attribute vec4 Position;
            uniform float Slice;
            varying float gLayer;
            uniform float Size;
            varying float vSize;
            void main(void){
                gl_Position = Position;
                gl_Position.x = gl_Position.x  / Size ;
                gl_Position.x = gl_Position.x +(2.0 * Slice + 1.0) / Size - 1.0; 
                gl_Position.zw =vec2(0.0, 1.0);
                gLayer = Slice ;
                vSize =Size;
            }
        </script>  
        <script id="Smoke.Fill" type="x-shader/x-fragment">  
            precision mediump float;
            void main(void){
                gl_FragColor  = vec4(1.0, 0.0, 0.0, 1.0);
            } 
        </script> 

        <script id="Sphere.VS" type="x-shader/x-vertex">  
            attribute vec3 Position;
            attribute vec3 Normal;
            varying vec3 vPosition;
            varying vec3 vNormal;
            uniform mat4 ModelviewProjection;

            void main()
            {
                vNormal = Normal;
                vPosition = Position.xyz;
                gl_Position = ModelviewProjection * vec4(Position, 1.0);
            }
        </script>  
        <script id="Sphere.FS" type="x-shader/x-fragment">  
            precision mediump float;
            varying vec3 vNormal;

            uniform mat3 ViewMatrix;
            uniform vec3 LightPosition;
            uniform vec3 DiffuseMaterial;
            uniform vec3 AmbientMaterial;
            uniform vec3 SpecularMaterial;
            uniform float Shininess;
            uniform mat3 NormalMatrix;

            vec3 ComputeLight(vec3 L, vec3 N, bool specular)
            {
                L = ViewMatrix * normalize(L);
                float df = max(0.0,dot(N, L));
                vec3 color = df * DiffuseMaterial;
                if (df > 0.0 && specular) {
                    vec3 E = vec3(0, 0, 1);
                    vec3 R = reflect(-L, N);
                    float sf = max(0.0,dot(R, E));
                    sf = pow(sf, Shininess);
                    color += sf * SpecularMaterial;
                }
                return color;
            }

            void main()
            {
                //vec3 N = NormalMatrix * normalize(gFacetNormal);
                vec3 N = NormalMatrix * normalize(vNormal);

                vec3 color = AmbientMaterial;
                color += 0.5 * ComputeLight(LightPosition, N, true);

                vec3 cameraLight = vec3(0, 0, 1) * ViewMatrix;
                color += 0.5 * ComputeLight(cameraLight, N, false);

                gl_FragColor = vec4(color, 1.0);
            }
        </script>

        <script id="Advect" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D VelocityTexture;
            uniform sampler2D SourceTexture;
            uniform sampler2D Obstacles;
            uniform vec3 InverseSize;
            uniform float TimeStep;
            uniform float Dissipation;
            varying float gLayer;
            varying float vSize;

            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size, texCoord.y));
               return Color;
            }
            void main(void){
                vec3 fragCoord = vec3(gl_FragCoord.x  - gLayer / InverseSize.x,gl_FragCoord.y, gLayer);
                float solid = sampleAs3DTexture(Obstacles, InverseSize * fragCoord, vSize).x;
                if (solid > 0.0) {
                    gl_FragColor = vec4(0);
                    return;
                }
                vec3 u = sampleAs3DTexture(VelocityTexture, InverseSize * fragCoord, vSize).xyz;
                vec3 coord = InverseSize * (fragCoord - TimeStep * u);
                gl_FragColor = Dissipation * sampleAs3DTexture(SourceTexture, coord, vSize);
                //gl_FragColor = Dissipation * texture2D(SourceTexture, coord.xy);
            } 
        </script>  
        <script id="Jacobi" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D Pressure;
            uniform sampler2D Divergence;
            uniform sampler2D Obstacles;

            uniform float Alpha;
            uniform float InverseBeta;
            uniform vec3 Scale;
            varying float vSize;
            varying float gLayer;
            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size, texCoord.y));
               return Color;
            }

            void main(void){
                vec3 T = vec3(gl_FragCoord.x  - gLayer / Scale.x,gl_FragCoord.y, gLayer);

                // Find neighboring pressure:
                vec4 pN = sampleAs3DTexture(Pressure, vec3(T.x,T.y+1.0, T.z)* Scale, vSize);
                vec4 pS = sampleAs3DTexture(Pressure, vec3(T.x,T.y-1.0, T.z)* Scale, vSize);
                vec4 pE = sampleAs3DTexture(Pressure, vec3(T.x+1.0,T.y, T.z)* Scale, vSize);
                vec4 pW = sampleAs3DTexture(Pressure, vec3(T.x-1.0,T.y, T.z)* Scale, vSize);
                vec4 pU = sampleAs3DTexture(Pressure, vec3(T.x,T.y, T.z+1.0)* Scale, vSize);
                vec4 pD = sampleAs3DTexture(Pressure, vec3(T.x,T.y, T.z-1.0)* Scale, vSize);
                vec4 pC = sampleAs3DTexture(Pressure, vec3(T.x,T.y, T.z)* Scale, vSize);

                // Find neighboring obstacles:
                vec3 oN = sampleAs3DTexture(Obstacles, vec3(T.x,T.y+1.0, T.z)* Scale, vSize).xyz;
                vec3 oS = sampleAs3DTexture(Obstacles, vec3(T.x,T.y-1.0, T.z)* Scale, vSize).xyz;
                vec3 oE = sampleAs3DTexture(Obstacles, vec3(T.x+1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 oW = sampleAs3DTexture(Obstacles, vec3(T.x-1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 oU = sampleAs3DTexture(Obstacles, vec3(T.x,T.y, T.z+1.0)* Scale, vSize).xyz;
                vec3 oD = sampleAs3DTexture(Obstacles, vec3(T.x,T.y, T.z-1.0)* Scale, vSize).xyz;

                // Use center pressure for solid cells:
                if (oN.x > 0.0) pN = pC;
                if (oS.x > 0.0) pS = pC;
                if (oE.x > 0.0) pE = pC;
                if (oW.x > 0.0) pW = pC;
                if (oU.x > 0.0) pU = pC;
                if (oD.x > 0.0) pD = pC;

                vec4 bC = sampleAs3DTexture(Divergence, vec3(T.x,T.y, T.z)* Scale, vSize);
                gl_FragColor = (pW + pE + pS + pN + Alpha * bC) * InverseBeta;
            } 
        </script>  



        <script id="SubtractGradient" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D Velocity;
            uniform sampler2D Pressure;
            uniform sampler2D Obstacles;
            uniform float GradientScale;
            varying float gLayer;
            uniform vec3 Scale;
            varying float vSize;

            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size , texCoord.y));
               return Color;
            }

            void main(void){
                vec3 T = vec3(gl_FragCoord.x  - gLayer / Scale.x,gl_FragCoord.y, gLayer);

                vec3 oC = sampleAs3DTexture(Obstacles, vec3(T.x,T.y,T.z)* Scale, vSize).xyz;
                if (oC.x > 0.0) {
                    gl_FragColor = vec4(oC.yzx, 1.0);
                    return;
                }

                // Find neighboring pressure:
                float pN = sampleAs3DTexture(Pressure, vec3(T.x,T.y+1.0, T.z)* Scale, vSize).r;
                float pS = sampleAs3DTexture(Pressure, vec3(T.x,T.y-1.0, T.z)* Scale, vSize).r;
                float pE = sampleAs3DTexture(Pressure, vec3(T.x+1.0,T.y, T.z)* Scale, vSize).r;
                float pW = sampleAs3DTexture(Pressure, vec3(T.x-1.0,T.y, T.z)* Scale, vSize).r;
                float pU = sampleAs3DTexture(Pressure, vec3(T.x,T.y, T.z+1.0)* Scale, vSize).r;
                float pD = sampleAs3DTexture(Pressure, vec3(T.x,T.y, T.z-1.0)* Scale, vSize).r;
                float pC = sampleAs3DTexture(Pressure, vec3(T.x,T.y, T.z)* Scale, vSize).r;

                // Find neighboring obstacles:
                vec3 oN = sampleAs3DTexture(Obstacles, vec3(T.x,T.y+1.0, T.z)* Scale, vSize).xyz;
                vec3 oS = sampleAs3DTexture(Obstacles, vec3(T.x,T.y-1.0, T.z)* Scale, vSize).xyz;
                vec3 oE = sampleAs3DTexture(Obstacles, vec3(T.x+1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 oW = sampleAs3DTexture(Obstacles, vec3(T.x-1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 oU = sampleAs3DTexture(Obstacles, vec3(T.x,T.y, T.z+1.0)* Scale, vSize).xyz;
                vec3 oD = sampleAs3DTexture(Obstacles, vec3(T.x,T.y, T.z-1.0)* Scale, vSize).xyz;

                // Use center pressure for solid cells:
                vec3 obstV = vec3(0);
                vec3 vMask = vec3(1);

                if (oN.x > 0.0) { pN = pC; obstV.y = oN.z; vMask.y = 0.0; }
                if (oS.x > 0.0) { pS = pC; obstV.y = oS.z; vMask.y = 0.0; }
                if (oE.x > 0.0) { pE = pC; obstV.x = oE.y; vMask.x = 0.0; }
                if (oW.x > 0.0) { pW = pC; obstV.x = oW.y; vMask.x = 0.0; }
                if (oU.x > 0.0) { pU = pC; obstV.z = oU.x; vMask.z = 0.0; }
                if (oD.x > 0.0) { pD = pC; obstV.z = oD.x; vMask.z = 0.0; }

                // Enforce the free-slip boundary condition:
                vec3 oldV = sampleAs3DTexture(Velocity, vec3(T.x,T.y,T.z)* Scale, vSize).xyz;
                vec3 grad = vec3(pE - pW, pN - pS, pU - pD) * GradientScale;
                vec3 newV = oldV - grad;
                gl_FragColor = vec4((vMask * newV) + obstV, 1.0);  
            }
        </script>  
        <script id="ComputeDivergence" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D Velocity;
            uniform sampler2D Obstacles;
            uniform float HalfInverseCellSize;
            varying float gLayer;
            uniform vec3 Scale;
            varying float vSize;

            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size, texCoord.y));
               return Color;
            }
            void main(){
                vec3 T = vec3(gl_FragCoord.x  - gLayer / Scale.x,gl_FragCoord.y, gLayer);
                // Find neighboring velocities:
                vec3 vN = sampleAs3DTexture(Velocity, vec3(T.x,T.y+1.0, T.z)* Scale, vSize).xyz;
                vec3 vS = sampleAs3DTexture(Velocity, vec3(T.x,T.y-1.0, T.z)* Scale, vSize).xyz;
                vec3 vE = sampleAs3DTexture(Velocity, vec3(T.x+1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 vW = sampleAs3DTexture(Velocity, vec3(T.x-1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 vU = sampleAs3DTexture(Velocity, vec3(T.x,T.y, T.z+1.0)* Scale, vSize).xyz;
                vec3 vD = sampleAs3DTexture(Velocity, vec3(T.x,T.y, T.z-1.0)* Scale, vSize).xyz;

                // Find neighboring obstacles:
                vec3 oN = sampleAs3DTexture(Obstacles, vec3(T.x,T.y+1.0, T.z)* Scale, vSize).xyz;
                vec3 oS = sampleAs3DTexture(Obstacles, vec3(T.x,T.y-1.0, T.z)* Scale, vSize).xyz;
                vec3 oE = sampleAs3DTexture(Obstacles, vec3(T.x+1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 oW = sampleAs3DTexture(Obstacles, vec3(T.x-1.0,T.y, T.z)* Scale, vSize).xyz;
                vec3 oU = sampleAs3DTexture(Obstacles, vec3(T.x,T.y, T.z+1.0)* Scale, vSize).xyz;
                vec3 oD = sampleAs3DTexture(Obstacles, vec3(T.x,T.y, T.z-1.0)* Scale, vSize).xyz;

                // Use obstacle velocities for solid cells:
                if (oN.x > 0.0) vN = oN.yzx;
                if (oS.x > 0.0) vS = oS.yzx;
                if (oE.x > 0.0) vE = oE.yzx;
                if (oW.x > 0.0) vW = oW.yzx;
                if (oU.x > 0.0) vU = oU.yzx;
                if (oD.x > 0.0) vD = oD.yzx;

                gl_FragColor = vec4(HalfInverseCellSize * (vE.x - vW.x + vN.y - vS.y + vU.z - vD.z),0.0, 0.0, 1.0);
            }
        </script> 
        <script id="Splat" type="x-shader/x-fragment">  
            precision mediump float;
            uniform vec3 Point;
            uniform float Radius;
            uniform vec3 FillColor;
            varying float gLayer;
            varying float vSize;
            uniform vec3 Scale;
            void main(){
                vec3 T =vec3(gl_FragCoord.x  - gLayer / Scale.x, gl_FragCoord.y, (gLayer - vSize /2.0) /vSize * Radius);
                float d = distance(Point, T);
                if (d < Radius) {
                    float a = (Radius - d) * 0.5;
                    a = min(a, 1.0);
                    gl_FragColor = vec4(FillColor, a);
                } else {
                    gl_FragColor = vec4(0);
                }
            }
        </script> 
        <script id="Buoyancy" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D Velocity;
            uniform sampler2D Temperature;
            uniform sampler2D Density;
            uniform float AmbientTemperature;
            uniform float TimeStep;
            uniform float Sigma;
            uniform float Kappa;

            varying float gLayer;
            uniform vec3 Scale;
            varying float vSize;

            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size , texCoord.y));
               return Color;
            }

            void main(){
                vec3 TC = vec3(gl_FragCoord.x  - gLayer / Scale.x,gl_FragCoord.y, gLayer);
                float T = sampleAs3DTexture(Temperature, vec3(TC.x,TC.y,TC.z)* Scale, vSize).r;
                vec3 V = sampleAs3DTexture(Velocity, vec3(TC.x,TC.y,TC.z)* Scale, vSize).xyz;

                gl_FragColor = vec4(V, 1.0);

                if (T > AmbientTemperature) {
                    float D = sampleAs3DTexture(Density, vec3(TC.x,TC.y,TC.z)* Scale, vSize).x;
                    gl_FragColor += vec4((TimeStep * (T - AmbientTemperature) * Sigma - D * Kappa ) * vec3(0, 1, 0), 1.0);
                }
            }
        </script> 
        <script id="Visualize" type="x-shader/x-fragment">  
            precision mediump float;
            uniform sampler2D Sampler;
            uniform vec3 FillColor;
            varying float gLayer;
            uniform vec3 Scale;
            varying float vSize;
            vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
               vec4 Color = texture2D(tex, vec2(texCoord.x  / size + texCoord.z, texCoord.y));
               return Color;
            }
            void main(){
                vec3 T = vec3(gl_FragCoord.xy, gLayer);
                float L = sampleAs3DTexture(Sampler, vec3(T.x,T.y, T.z) * Scale, vSize).r;
                gl_FragColor = vec4(FillColor, L);
            } 
        </script> 

        <style>
            * {
                margin: 0 auto;
                margin-top: 1px;
                text-align: center;
            }
        </style>
    </head>  
    <body>  
        <canvas id="canvas"></canvas>  
       
        <script src="main.js" type="text/javascript"></script>  
    </body>  
</html>  